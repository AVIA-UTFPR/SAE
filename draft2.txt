GWENDOLEN

:name: Car

:Initial Beliefs:

min(0,0)

:Reasoning Rules:

arrived(X,Y) :- destination(X,Y), at(X,Y);
drive_to(X,Y) :- arrived(X,Y);
finish_all_rides :- done_all_rides;

position_grid(AT_X, AT_Y, D_X, D_Y) :- at(AT_X, AT_Y), destination(D_X, D_Y);

// ------------------ Directions ------------------

north_south :- north;
north_south :- south;
east_west :- east;
east_west :- west;

// ------------------ Ride information ------------------
current_pick_up(X,Y) :- ride_info, pick_up(X,Y);
current_drop_off(X,Y) :- ride_info, drop_off(X,Y);

journey_not_possible(X,Y) :-verify_journey(X,Y), ~possible_journey_to(X,Y);

reach(X,Y) :- try_to_reach(X,Y), at(X,Y);

// Drive

turn(north) :- heading(north), destination(X,Y), at(AT_X, Y);
turn(south) :- heading(south), destination(X,Y), at(AT_X, Y);

turn(east) :- heading(east), east, destination(X,Y), at(X, AT_Y);
turn(west) :- heading(west), west, destination(X,Y), at(X, AT_Y);

// Avoid Obstacles

obstacle_ahead(DIRECTION) :- at(AT_X, AT_Y), obstacle(DIRECTION, AT_X, AT_Y);

can_adapt(DIRECTION) :- at(AT_X, AT_Y), ~obstacle(DIRECTION, AT_X, AT_Y), ~known_route(DIRECTION);
can_adapt_simple(DIRECTION) :- can_adapt(DIRECTION), go(DIRECTION);

go(north_south)	:- ~heading(north), ~heading(south), ~adapt(east), ~adapt(west);
go(east_west) 	:- ~heading(east), ~heading(west), ~adapt(north), ~adapt(south);

go(north) :- ~adapt(south), go(north_south);
go(south) :- ~adapt(north), go(north_south);
go(east) :- ~adapt(west), go(east_west);
go(west) :- ~adapt(east), go(east_west);

known_route(north) :- at(AT_X, AT_Y), destination(D_X, D_Y), adapt_from_to(AT_X, AT_Y, north, D_X, D_Y), moved_from_to(AT_X, AT_Y, south, D_X, D_Y);
known_route(south) :- at(AT_X, AT_Y), destination(D_X, D_Y), adapt_from_to(AT_X, AT_Y, south, D_X, D_Y), moved_from_to(AT_X, AT_Y, north, D_X, D_Y);
known_route(east) :- at(AT_X, AT_Y), destination(D_X, D_Y), adapt_from_to(AT_X, AT_Y, east, D_X, D_Y), moved_from_to(AT_X, AT_Y, west, D_X, D_Y);
known_route(west) :- at(AT_X, AT_Y), destination(D_X, D_Y), adapt_from_to(AT_X, AT_Y, west, D_X, D_Y), moved_from_to(AT_X, AT_Y, east, D_X, D_Y);

no_route(DIRECTION) :- at(AT_X, AT_Y), known_route(DIRECTION);

// Emergency
control_emergency (X,Y) :- ~crashed(X,Y); // Car recovered from crash in (X,Y)


:Initial Goals:

finish_all_rides [achieve]

:Plans:


// ---------------------------------------------- Finish all Rides - !finish_all_rides [achieve] ----------------------------------------------

// Get location of the car in the environment
+!finish_all_rides [achieve] : {~B at(X,Y)} <- localize, *at(GPS_X, GPS_Y);

// If no passengers is available, car goes back to depot
+!finish_all_rides [achieve] : {B no_possible_new_ride, B depot(X,Y)} <- +!complete_journey (X, Y) [perform], message(depot), +done_all_rides;

// Get New Ride
+!finish_all_rides [achieve] : {~B ride_info} <- get_ride, *ride_info;


// Verify it can reach destinations of the new ride before it tries to pick up the passanger
//+!finish_all_rides [achieve] : {B current_pick_up(X,Y),  ~B verify_journey(X,Y)}
//								<- +!verify_destination (X,Y) [perform], +verify_journey(X,Y);
//+!finish_all_rides [achieve] : {B current_pick_up(X,Y),  B journey_not_possible(X,Y)}
//								<- refuse_ride(pick_up), +!clear_current_ride [perform];

//+!finish_all_rides [achieve] : {B current_drop_off(X,Y), ~B verify_journey(X,Y)}
//								<- +!verify_destination (X,Y) [perform], +verify_journey(X,Y);
//+!finish_all_rides [achieve] : {B current_drop_off(X,Y), B journey_not_possible(X,Y)}
//								<- refuse_ride(drop_off), +!clear_current_ride [perform];

+!finish_all_rides [achieve] : {B pick_up(X,Y),  B obstacle(center, X, Y)}
								<- refuse_ride(pick_up), -pick_up(X,Y), -ride_info;
+!finish_all_rides [achieve] : {B drop_off(X,Y), B obstacle(center, X, Y)}
								<- refuse_ride(drop_off), -drop_off(X,Y), -ride_info;

// ---------------------------------------------- Pick Up ----------------------------------------------


// Current ride - Pick up
+!finish_all_rides [achieve] : {B current_pick_up(X,Y), ~B try_to_reach(X,Y), ~B passenger}
								<- 	+!complete_journey (X,Y) [perform],
									+try_to_reach(X,Y);

+!finish_all_rides [achieve] : {B current_pick_up(X,Y), B reach(X,Y), ~B passenger}
								<- 	park(pick_up), +passenger,
									-pick_up(X,Y), -try_to_reach(X,Y);

+!finish_all_rides [achieve] : {B current_pick_up(X,Y), ~B reach(X,Y), ~B passenger}
								<- 	refuse_ride(pick_up), -pick_up(X,Y),
									-try_to_reach(X,Y), -ride_info;


// ---------------------------------------------- Drop Off ----------------------------------------------

// Current ride - Drop off
+!finish_all_rides [achieve] : {B current_drop_off(X,Y), ~B try_to_reach(X,Y), B passenger}
								<- 	+!complete_journey (X,Y) [perform],
									+try_to_reach(X,Y);


+!finish_all_rides [achieve] : {B current_drop_off(X,Y), B reach(X,Y), B passenger}
								<- 	park(drop_off), -passenger,
									-drop_off(X,Y), -try_to_reach(X,Y), -ride_info;

+!finish_all_rides [achieve] : {B current_drop_off(X,Y), ~B reach(X,Y), B passenger}
								<- 	refuse_ride(drop_off), park(drop_off), -passenger,
									-drop_off(X,Y), -try_to_reach(X,Y), -ride_info;


+!clear_current_ride [perform] : {B pick_up(PK_X,PK_Y), B drop_off(DP_X,DP_Y)} <- -pick_up(PK_X,PK_Y), -drop_off(DP_X,DP_Y), -ride_info;


// ---------------------------------------------- Verifying Destination - !verify_destination(V_X,V_y) ----------------------------------------------

// Verify if can reach destination
+!verify_destination (V_X,V_Y) [perform] : {B min(MIN_X, MIN_Y), V_X < MIN_X}  ;	//print("cant reach destination MIN X")
+!verify_destination (V_X,V_Y) [perform] : {B min(MIN_X, MIN_Y), V_Y < MIN_Y}  ;	//print("cant reach destination MIN Y")
+!verify_destination (V_X,V_Y) [perform] : {B max(MAX_X, MAX_Y), MAX_X < V_X}  ;	//print("cant reach destination MAX X")
+!verify_destination (V_X,V_Y) [perform] : {B max(MAX_X, MAX_Y), MAX_Y < V_Y}  ;	//print("cant reach destination MAX Y")

// if no previous GUARD matched, then the journey is possible
+!verify_destination (V_X,V_Y) [perform] : {True}  <- +possible_journey_to(V_X,V_Y);

// ---------------------------------------------- Complete Journey ----------------------------------------------


// Go from current position to at(X,Y)
+!complete_journey (X,Y) [perform] : {True} <- 	+!clear_travel_data [perform],
												+destination(X,Y),
												+moving, +!drive_to(X, Y) [achieve], -moving,
												-destination(X, Y);

+!clear_travel_data [perform] : {True} <- 	+!clear_direction_data [perform],
											-heading(north), -heading(south), -heading(east), -heading(west);

+!clear_direction_data [perform] : {True} <-
									-adapt(north), -adapt(south),  -adapt(east), -adapt(west),
									-north, -south, -east, -west,
									-receive_direction;

+!get_route [perform] : {B destination(X,Y)} <- +!clear_travel_data [perform], compass(X, Y), *receive_direction;

// ---------------------------------------------- Drive - !drive_to(X,Y) ----------------------------------------------


// Choose first direction to go
+!drive_to(X,Y) [achieve] : {~B north, ~B south, ~B east, ~B west} <- +!get_route [perform];

// Decide where the car should head to
+!drive_to(X,Y) [achieve] : {~B heading(H), B north} <- +heading(north);
+!drive_to(X,Y) [achieve] : {~B heading(H), B south} <- +heading(south);
+!drive_to(X,Y) [achieve] : {~B heading(H), B east}  <- +heading(east);
+!drive_to(X,Y) [achieve] : {~B heading(H), B west}  <- +heading(west);

// If car needs to turn
+!drive_to(X,Y) [achieve] : {B turn(north), B east}  <- -heading(north), +heading(east); // north to east
+!drive_to(X,Y) [achieve] : {B turn(north), B west}  <- -heading(north), +heading(west); // north to west

+!drive_to(X,Y) [achieve] : {B turn(south), B east}  <- -heading(south), +heading(east); // south to east
+!drive_to(X,Y) [achieve] : {B turn(south), B west}  <- -heading(south), +heading(west); // south to west

// If car is blocked or can't reach destination, drop the goal
+!drive_to(X,Y) [achieve] : {B blocked} <- -blocked, -!drive_to(X,Y) [achieve];
+!drive_to(X,Y) [achieve] : {B obstacle(center, X, Y)} <- -!drive_to(X,Y) [achieve];

// Wait to be in movement to drive
+!drive_to(X,Y) [achieve] : {~B moving} <- *moving;

// Obstacle Avoidance
+!drive_to(X,Y) [achieve] : {B heading(D), B obstacle_ahead(D)}
							<- -moving, +!adapt_route(D) [achieve];

// When the car believes it can just move
+!drive_to(X,Y) [achieve] : {B heading(D), ~B obstacle_ahead(D)} <- +!drive_direction(D) [perform]; //print(normalDrive),


// ---------------------------------------- Drive Direction ----------------------------------------

+!drive_direction(D) [perform] : { G drive_to(X, Y) [achieve] }
						<- -moving, drive(D), *at(NEW_X, NEW_Y), -moving, +moved_from_to(NEW_X, NEW_Y, D, X, Y), +moving;

// ---------------------------------------- Obstacle Avoidance and adapt Route - !adapt_route(D, X, Y) [achieve]  ----------------------------------------


+!adapt_route(D) [achieve] : {B obstacle_ahead(north), B obstacle_ahead(south), B obstacle_ahead(east), B obstacle_ahead(west)}
							<- +blocked, -!adapt_route(D) [achieve];

+!adapt_route(D) [achieve] : {B can_adapt(D)}
							<- 	-moving,
								+!drive_direction(D) [perform],  +!get_route [perform], //message(fixed),
								-!adapt_route(D) [achieve];

// ------------------------------ North or South - Avoid Simple  ------------------------------


// Simple Belief North or South to East or West
+!adapt_route(D) [achieve] : {B can_adapt_simple(east), B east, B north_south}
								<- message("Simple Belief North or South to East"), +!adapt_drive_direction(east) [perform];
+!adapt_route(D) [achieve] : {B can_adapt_simple(west), B west, B north_south}
								<- message("Simple Belief North or South to West"), +!adapt_drive_direction(west) [perform];

+!adapt_route(D) [achieve] : {B can_adapt_simple(east), B north_south}
									<- message("Reroute North or South to East"), +!adapt_drive_direction(east) [perform];
+!adapt_route(D) [achieve] : {B can_adapt_simple(west), B north_south}
									<- message("Reroute North or South to West"), +!adapt_drive_direction(west) [perform];

// ------------------------------ East or West - Avoid Simple  ------------------------------

//Simple Belief East or West to North or South
// If it came from North, adapt South. And vice-versa
+!adapt_route(D) [achieve] : {B north, B can_adapt_simple(south), B east_west}
									<- message("Simple Belief East or West to South"), +!adapt_drive_direction(south) [perform];
+!adapt_route(D) [achieve] : {B south, B can_adapt_simple(north), B east_west}
									<- message("Simple Belief East or West to North"), +!adapt_drive_direction(north) [perform];


+!adapt_route(D) [achieve] : {B can_adapt_simple(north), B east_west}
									<- message("Reroute East or West to North"), +!adapt_drive_direction(north) [perform];
+!adapt_route(D) [achieve] : {B can_adapt_simple(south), B east_west}
									<- message("Reroute East or West to South"), +!adapt_drive_direction(south) [perform];




// ------------------------------ East or West - Avoid Obstacles while re rerouting  ------------------------------


//Reroute the reroute from East or West to North
+!adapt_route(east) [achieve] : {~B can_adapt(north), B adapt(north), B can_adapt(south), G drive_to(X, Y) [achieve]}
								<- message("Cant keep going north"),
								-adapt(north),
								no_further_info(east, X, Y),
								+!go_back(south) [perform];

+!adapt_route(west) [achieve] : {~B can_adapt(north), B adapt(north), B can_adapt(south), G drive_to(X, Y) [achieve]}
								<- message("Cant keep going north"),
								-adapt(north),
								no_further_info(west, X, Y),
								+!go_back(south) [perform];


//Reroute the reroute from East or West to North
+!adapt_route(east) [achieve] : {~B can_adapt(south), B adapt(south), B can_adapt(north), G drive_to(X, Y) [achieve]}
								<- message("Cant keep going south"),
								+!clear_adapt [perform],
								no_further_info(east, X, Y),
								+!go_back(north) [perform];

+!adapt_route(west) [achieve] : {~B can_adapt(south), B adapt(south), B can_adapt(north), G drive_to(X, Y) [achieve]}
								<- message("Cant keep going south"),
								+!clear_adapt [perform],
								no_further_info(west, X, Y),
								+!go_back(north) [perform];

// Return
+!adapt_route(east) [achieve] : {~B can_adapt(north), ~B can_adapt(south), B can_adapt(west)}
								<- message("Cant keep going east"),
								+!clear_adapt [perform],
								+!go_back(west) [perform];
// Return
+!adapt_route(west) [achieve] : {~B can_adapt(north), ~B can_adapt(south), B can_adapt(east)}
								<- message("Cant keep going west"),
								+!clear_adapt [perform],
								+!go_back(east) [perform];

+!clear_adapt [perform] : {True} <- -adapt(north), -adapt(south), -adapt(east), -adapt(west);

// ------------------------------------------------------------
+!adapt_route(D) [achieve] : {True}
				<-	message("                                   "),
					message("new_adapt_rule adapt_route_true"),
					message("                                   "),
					message(D), +blocked, -!adapt_route(D) [achieve];

// --------------------------------------------------------------------------------

+!adapt_drive_direction(D) [perform] : {G drive_to(X, Y) [achieve], B at(AT_X, AT_Y)}
							<- +adapt_from_to(AT_X, AT_Y, D, X, Y), +adapt(D), +!drive_direction(D) [perform];

// --------------------------------------------------------------------------------
+!go_back(north) [perform]: {G drive_to(X, Y) [achieve]}
						<- 	-moving, drive(north), *at(AT_X, AT_Y), go_back(north, X, Y);

+!go_back(south) [perform]: {G drive_to(X, Y) [achieve]}
						<- 	-moving, drive(south), *at(AT_X, AT_Y), go_back(south, X, Y);

+!go_back(east) [perform]: {G drive_to(X, Y) [achieve]}
						<- 	-moving, drive(east), *at(AT_X, AT_Y), go_back(east, X, Y);

+!go_back(west) [perform]: {G drive_to(X, Y) [achieve]}
						<- 	-moving, drive(west), *at(AT_X, AT_Y), go_back(west, X, Y);

// ---------------------------------------- adapt_from_to - moved_from_to ----------------------------------------
// north

+moved_from_to(X, Y, south, D_X, D_Y) : {B adapt_from_to(X, Y, north, D_X, D_Y)} <- no_further_info(east, D_X, D_Y), no_further_info(west, D_X, D_Y);
+adapt_from_to(X, Y, north, D_X, D_Y) : {B moved_from_to(X, Y, south, D_X, D_Y)} <- no_further_info(east, D_X, D_Y), no_further_info(west, D_X, D_Y);


+moved_from_to(X, Y, north, D_X, D_Y) : {B adapt_from_to(X, Y, south, D_X, D_Y)} <- no_further_info(east, D_X, D_Y), no_further_info(west, D_X, D_Y);
+adapt_from_to(X, Y, south, D_X, D_Y) : {B moved_from_to(X, Y, north, D_X, D_Y)} <- no_further_info(east, D_X, D_Y), no_further_info(west, D_X, D_Y);

// south
//+adapt_from_to(X, Y, north, D_X, D_Y) : {B moved_from_to(X, Y, south, D_X, D_Y)} <- message(oops), no_further_info(X, Y, south, D_X, D_Y);
//+moved_from_to(X, Y, south, D_X, D_Y) : {B adapt_from_to(X, Y, north, D_X, D_Y)} <- message(oops), no_further_info(X, Y, south, D_X, D_Y);

//+adapt_from_to(2, 0, D, D_X, D_Y) : {True} <- message(adapt), message("2 0"), message(D_X), message(D_Y), message(D);
//+moved_from_to(2, 0, D, D_X, D_Y) : {True} <- message(move), message("2 0"), message(D_X), message(D_Y), message(D);


// ---------------------------------------- obstacle(D, X,Y)  ----------------------------------------
+obstacle(center, X, Y) : {G drive_to(X,Y) [achieve]} <- -!drive_to(X,Y) [achieve], -!adapt_route(D) [achieve];

// ---------------------------------------- at(X,Y)  ----------------------------------------

+at(X,Y) : {B obstacle(center, X,Y)} <- -moving, +crashed(X,Y), +!control_emergency (X,Y) [achieve], +moving;
+at(X,Y) : {~B obstacle(center, X,Y)} <- +moving;


// ---------------------------------------- Control Emergencies - !control_emergency (X,Y)  ----------------------------------------
// If Crashed, ask for help
+!control_emergency (X,Y) [achieve] : {B help(X,Y), B alert_state} <-  -help(X,Y), -alert_state, -crashed(X,Y);
+!control_emergency (X,Y) [achieve] : {B crashed(X,Y), ~B alert_state} <- call_emergency(X,Y), *help(X,Y), +alert_state;
