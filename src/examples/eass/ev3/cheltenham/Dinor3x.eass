// ----------------------------------------------------------------------------
// Copyright (C) 2012 Louise A. Dennis, and  Michael Fisher 
//
// This file is part of the Engineering Autonomous Space Software (EASS) Library.
// 
// The EASS Library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 3 of the License, or (at your option) any later version.
// 
// The EASS Library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
// Lesser General Public License for more details.
// 
// You should have received a copy of the GNU Lesser General Public
// License along with the EASS Library; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
// 
// To contact the authors:
// http://www.csc.liv.ac.uk/~lad
//
//----------------------------------------------------------------------------
EASS

:abstraction: dinor3x

:Initial Beliefs:

distance_threshold(0.5)
water_threshold_upper(0.15)
water_threshold_lower(0.1)
path_threshold(0.05)

:Initial Goals:

:Plans:
/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];

+distance(D) : {~B obstacle, B distance_threshold(V), D < V} <- assert_shared(obstacle);
+distance(D) : {B obstacle, B distance_threshold(V), V < D} <- remove_shared(obstacle);
+blue(L) : {~B water, B water_threshold_upper(V), B water_threshold_lower(V2), V2 < L,  L < V} <- assert_shared(water);
+blue(L) : {B water, B water_threshold_lower(V), L < V} <- remove_shared(water);
+blue(L) : {B water, B water_threshold_upper(V), V < L} <- remove_shared(water);

+red(L) : {~B path, B path_threshold(V), L < V} <- assert_shared(path);
+red(L) : {B path, B path_threshold(V), V < L} <-
	  remove_shared(path);

+! stop [perform] : {True} <-
	stop;
+! right [perform] : {True} <-
    right;
+! left [perform] : {True} <-
    left;
+! backward [perform] : {True} <-
    backward;
+! forward [perform] : {True} <-
    forward;
+! do_nothing [perform] : {True} <-
    do_nothing;

+change_distance(D) : {B distance_threshold(D1)} <-
    -distance_threshold(D1), +distance_threshold(D);

:name: dinor3x

:Initial Beliefs:

have_belief(anything)

:Reasoning Rules:
have_belief(no_water) :- ~ have_belief(water);

:Initial Goals:

:Plans:

+obstacle : {B active(rule1), B context(rule1, X), B have_belief(X), B rule(rule1, act1, A), B rule(rule1, act2, B), B rule(rule1, act3, C)} <-
    show_belief(obstacle),
    perf(A),
    perf(B),
    perf(C);
-obstacle : {B active(rule2), B context(rule2, X), B have_belief(X), B rule(rule2, act1, A), B rule(rule2, act2, B), B rule(rule2, act3, C)} <-
    remove_belief(obstacle),
    perf(A),
    perf(B),
    perf(C);
+obstacle : {B active(rule3), B context(rule3, X), B have_belief(X), B rule(rule3, act1, A), B rule(rule3, act2, B), B rule(rule3, act3, C)} <-
    show_belief(obstacle),
    perf(A),
    perf(B),
    perf(C);
-obstacle : {B active(rule4), B context(rule4, X), B have_belief(X), B rule(rule4, act1, A), B rule(rule4, act2, B), B rule(rule4, act3, C)} <-
    remove_belief(obstacle),
    perf(A),
    perf(B),
    perf(C);

+obstacle : {True} <-
    show_belief(obstacle);
-obstacle : {True} <-
    remove_belief(obstacle);
    
+water : {True} <-
 	-! water [perform],
 	remove_goal(water),
 	remove_goal(path),
 	drop_goal,
 	+stop_looking_for_path,
 	+have_belief(water),
	show_belief(water);
-water : {True} <-
	-have_belief(water),
	remove_belief(water);

+path : {True} <-
	  +stop_looking_for_path,
      show_belief(path);
-path : {True} <-
      remove_belief(path);

+goal(scare) : {True} <-
		 show_goal(scare),
	     growl,
	     remove_goal(scare);

+goal(water) : {True} <-
		show_goal(water),
	     +! water [perform];
-goal(water) : {True} <-
		remove_goal(water),
	     -! water [perform];

+! water [perform] : {True} <-
   -stop_looking_for_path,
   show_goal(path),
   random,
   remove_goal(path),
   *stop_looking_for_path,
   -stop_looking_for_path,
   follow_line,
   *water,
   remove_belief(goal(water)),
   stop,
   drop_goal;


