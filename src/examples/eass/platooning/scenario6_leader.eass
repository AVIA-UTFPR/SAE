
:name: leader

:Initial Beliefs:

req(1) // i.e., request to join
req(0) // request to leave
req(2) // joined successfully
allowed_spot(follower1) // where in the platoon a vehicle is allowed to get in
allowed_spot(0) //joint to platoon from behind is allowed

:Reasoning Rules:
last_vehicle(Y):- platoon_m(X,Y), ~platoon_m(Y,Z);
no_platoon_m(X):- ~platoon_m(X, Y), ~platoon_m(Z, X); 
//platoon_m(Y, 0):- update_last_v, last_vehicle(Y);

:Initial Goals:

:Plans:

/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];

//=================================== leader receives a joining request, the requested spot is allowed and front vehicle is part of platoon================
+message(SENDER, REQ, FRONT): {B no_platoon_m(SENDER), ~B request_to_join(SENDER), B req(1), 1 == REQ, B allowed_spot(FRONT), ~B no_platoon_m(FRONT)} <- 
						+!platoon_m(SENDER, FRONT) [achieve],
						print(leader_vehicle_in_the_middle_successfuly_joined); 

//=================================== leader receives a joining request, joining from behind ================
+message(SENDER, REQ, FRONT): {B no_platoon_m(SENDER), ~B request_to_join(SENDER), B req(1), 1 == REQ, B allowed_spot(FRONT), FRONT== 0} <- 
						+!platoon_m(SENDER, FRONT) [achieve],
						print(leader_vehicle_from_behind_successfuly_joined); 

//=================================== leader receives a joining request, the requested spot is allowed but front vehicle is not part of platoon================
+message(SENDER, REQ, FRONT): {B no_platoon_m(SENDER), ~B request_to_join(SENDER), B req(1), 1 == REQ, B allowed_spot(FRONT), B no_platoon_m(FRONT), ~ FRONT == 0} <- 
						.send(SENDER, :tell, wrong_front),
						print(leader_front_is_not_part_of_platoon); 

//=================================== leader receives a joining request, the requested spot is not allowed ================
+message(SENDER, REQ, FRONT): {B no_platoon_m(SENDER), ~B request_to_join(SENDER), B req(1), 1 == REQ, ~B allowed_spot(FRONT)} <-
						.send(SENDER, :tell, wrong_front),
						print(leader_joining_in_requested_spot_is_not_allowed); 

// ==== for trouble shooting ======
+message(SENDER, REQ, FRONT): {~B no_platoon_m(SENDER), B req(1), 1 == REQ} <- 
						print(something_is_wrong_sender_already_in_platoon);

// ============== SENDER request to join to platoon and leader sets a goal to have SENDER in platoon =================
+!platoon_m(SENDER, FRONT) [achieve]: {~B set_spacing_from(FRONT), ~ FRONT == 0}<-
						.send(FRONT, :achieve, set_spacing(17)), *set_spacing_from(FRONT);

+!platoon_m(SENDER, FRONT) [achieve]: {FRONT == 0, ~B message(SENDER, 2)}<-
						.send(SENDER, :tell, join_agreement(SENDER, FRONT)), *message(SENDER, 2);
							
+!platoon_m(SENDER, FRONT) [achieve]: {B set_spacing_from(FRONT), ~B message(SENDER, 2)}<-
						.send(SENDER, :tell, join_agreement(SENDER, FRONT)), *message(SENDER, 2);

+!platoon_m(SENDER, FRONT) [achieve]: {B message(SENDER, REQ), B req(2), 2== REQ, B platoon_m(X, FRONT)}<- 
																.send(SENDER,:tell, platoon_m), 
																.send(FRONT, :achieve, set_spacing(6)),
																-platoon_m(X,FRONT), +platoon_m(X, SENDER), +platoon_m(SENDER, FRONT);

+!platoon_m(SENDER, FRONT) [achieve]: {B message(SENDER, REQ), B req(2), 2== REQ, FRONT == 0, B last_vehicle(Y)}<- 
																.send(SENDER,:tell, platoon_m), 
																+platoon_m(Y, SENDER), -!platoon_m(SENDER, FRONT) [achieve]// +update_last_v;
																


								