
/*
	Platooning Scenario 2

	A vehicle joins the platoon from behind, one vehicle RE + leader RE + simple joining protocol, Same for leaving platoon from behind. States to be monitored: joining-req, leaving-req

	A vehicle at the end of the platoon sends a join or leave request to the leader. In case of joining, the leader through a simple joining protocol decides to authorise the vehicle 
	to join and synchronise clocks. The vehicle receiving confirmation decides for itself to go through a series of switches in order to get to platoon control. 
	We will then verify if the vehicle gets a confirmation from the leader for joining then it switches to platoon control and if the vehicle doesn’t get a confirmation from the leader
	for joining then it stays on cruise control.

	There are some temporal properties such as “if in some certain amount of time the last vehicle gets to Ok-distance, meaning the joining protocol satisfies the joining requirement” 
	and “when a vehicle leaves the platoon, we can check after e.g., 2 sec., the vehicle is too-far from the platoon”. These remain for future work because at the moment we cannot 
	verify them in AJPF. We need to investigate on how to export models from AJPF to UPPAAL.

	Our assumption is that other vehicles do nothing. The focus is on coordination between the leader and a vehicle joining the platoon from behind.


	ASSUMPTION: while a car from behind is joining, no car can leave platoon
				last car has highest platoon_ID
				(Design Decision) I am sending messages through abstract engine; communication between agents is in abstraction engine level; two REs don't talk to each other directly
*/


EASS

//=================================== behind vehicle abstract engine ===============================================

:abstraction: follower1

:Initial Beliefs:
vehicle_id(3)
init_min_speed(10) // minimum initial speed to switch to speed control
init_max_dist(20) // maximum initial distance to switch to steering control
min_azimuth(10) // minimum azimuth to leave platoon

:Initial Goals:

:Plans:

/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];

//========== Input abstraction = {join_agreement, no_platoon_member, preceding_vehicle_platoon_member} ===========

+precedingPID(PID): {~B preceding_vehicle_platoon_member, 0 < PID}<- assert_shared(preceding_vehicle_platoon_member); // assume ID = 0 when a vehicle is not a member of the platoon
+precedingPID(PID): {B preceding_vehicle_platoon_member, PID < 1}<- remove_shared(preceding_vehicle_platoon_member); 

+egoPID(PID): {B no_platoon_member, 0 < PID}<- remove_shared(no_platoon_member);
+egoPID(PID): {~B no_platoon_member, PID == 0}<- assert_shared(no_platoon_member);

+speed(X): {~B initial_speed, B init_min_speed(Y), Y < X}<- assert_shared(initial_speed); // if vehicle's speed is greater than minimum initial speed, we add "initial_speed" belief
+speed(X): {B initial_speed, B init_min_speed(Y), X < Y}<- remove_shared(initial_speed); // if vehicle's speed is less than minimum initial speed, we remove "initial_speed" belief

+distance(X): {~B initial_distance, B init_max_dist(Y), Y < X }<- assert_shared(initial_distance); // if vehicle's distance is less than maximum initial distance, we add "initial_distance" belief
+distance(X): {B initial_distance, B init_max_dist(Y), X < Y }<- remove_shared(initial_distance); // if vehicle's distance is greater than maximum initial distance, we remove "initial_distance" belief

+azimuth(X): {~B no_platoon_member, ~B azimuth, B min_azimuth(Y), Y < X }<- assert_shared(azimuth);
+azimuth(X): {B azimuth, B min_azimuth(Y), X < Y }<- remove_shared(azimuth);


//========== Output reification = {switch_platoon_to_cruise, switch_cruise_to_platoon, request_to_join } =====

+!switch_to_speed_controller [perform]: {True}<- run(switch_to_speed_controller);

+!switch_to_steering_controller [perform]: {True}<- run(switch_to_steering_controller);

+!assign_platoon_id(X) [perform]: {True}<- run(pred(assign_platoon_id),args(X));



//=================================== behind vehicle reasoning engine ===============================================

:name: follower1

:Initial Beliefs:

//no_platoon_member
vehicle_id(3)
req(join)
req(leave)

:Reasoning Rules:

join_to_platoon_from_behind(X,Y):- ~ no_platoon_member;

:Initial Goals:

:Plans:

/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];

+no_platoon_member: {B preceding_vehicle_platoon_member, B vehicle_id(X), ~B request_to_join(X), B req(join)}<- .send(leader, :tell, message(X, 1)), +request_to_join(X), print(send_request_to_join);
+preceding_vehicle_platoon_member: {B no_platoon_member, B vehicle_id(X), ~B request_to_join(X), B req(join)}<- .send(leader, :tell, message(X, 1)), +request_to_join(X), print(send_request_to_join);

+azimuth: {~B no_platoon_member, ~B request_to_leave(X), B req(leave)}<- .send(leader, :tell, message(X, 0)), +request_to_leave(X), print(send_request_to_leave);


+!join_to_platoon_from_behind(X,Y) [achieve]: {B preceding_vehicle_platoon_member, B no_platoon_member, 
											B initial_speed, B initial_distance, B join_agreement(X,Y)}<- 
											perf(switch_to_speed_controller), +speed_controller,
											print(speed_controller_enabled);
				 
+!join_to_platoon_from_behind(X,Y) [achieve]: {B preceding_vehicle_platoon_member, B no_platoon_member,
											~B initial_distance, B join_agreement(X,Y), B speed_controller, B vehicle_id(K)}<- 
											perf(assign_platoon_id(Y)), 
											perf(switch_to_steering_controller), 
											remove_shared(no_platoon_member),
											remove_shared(join_agreement(X,Y)),
											-speed_controller,
											.send(leader, :tell, platoon_member(K)),
											print(switch_to_steering_controller),
											print(joining_to_platoon_completed);

+!join_to_platoon_from_behind(X,Y) [achieve]: {B preceding_vehicle_platoon_member, B no_platoon_member,
											B initial_speed, ~B initial_distance, B join_agreement(X,Y), ~B speed_controller, B vehicle_id(K)} <-
											perf(switch_to_speed_controller), +speed_controller,
											perf(assign_platoon_id(Y)), 
											perf(switch_to_steering_controller), 
											remove_shared(no_platoon_member),
											remove_shared(join_agreement(X,Y)),
											.send(leader, :tell, platoon_member(K)),
											print(speed_controller),
											print(steering_controller),
											print(joining_to_platoon_completed);
											

+!leave_platoon_from_behind [achieve]: {~B no_platoon_member} <- 
				perf(switch_platoon_to_cruise), assert_shared(no_platoon_member); 
			
+join_agreement(X,Y): {B no_platoon_member, B vehicle_id(X), B request_to_join(X) }<- -request_to_join(X), print(go_for_joining), +!join_to_platoon_from_behind(X,Y) [achieve];




//=================================== leader abstract engine ===============================================


:abstraction: leader

:Initial Beliefs:

platoon_member(1) platoon_member(2) 
//platoon_member(3)
//platoon_member(4) platoon_member(5) platoon_member(6)
//platoon_member(7) platoon_member(8)
req(join)
req(leave)


:Initial Goals:

:Plans:

/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];



+!time_synch(SENDER) [perform]: {True}<- run(time_synchronisation); 

//=================================== leader reasoning engine ===============================================


:name: leader

:Initial Beliefs:

platoon_member(1) platoon_member(2) 
//platoon_member(3)
//platoon_member(4) platoon_member(5) platoon_member(6)
//platoon_member(7) platoon_member(8)
req(1) // means request to join
req(0) // means request to leave
last_car(2)

:Initial Goals:

:Plans:

/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];

+message(SENDER, REQ): {~B platoon_member(SENDER), ~B request_to_join(SENDER), B req(1), 1 == REQ} <- 
						assert_shared(request_to_join(SENDER)),
						+accept_request_join(SENDER),
						print(leader_received_req); 

+message(SENDER, REQ): {B platoon_member(SENDER), ~B request_to_leave(SENDER), B req(0), 0 == REQ} <-
						assert_shared(request_to_leave(SENDER)),
						+accept_request_leave(SENDER),
						print(leader_received_req_leave); 

+platoon_member(SENDER): {~B platoon_member(SENDER), ~B request_to_join(SENDER), B last_car(X)}<- -accept_request_join(SENDER), -last_car(X), +last_car(SENDER), print(leader_knows_a_vehicle_joined_to_platoon);

+accept_request_join(SENDER): {~B platoon_member(SENDER), B last_car(Y), Y < SENDER, ~B ack_sent(SENDER), B request_to_join(SENDER)} <- 
									.send(follower1, :tell, join_agreement(SENDER,SENDER)), -request_to_join(SENDER), perf(time_synch(SENDER)),
									print(leader_sent_acknowledge_back_to_sender), -message(SENDER, 1);

 